print "Counting lines in codebase..."

function count_lines_in_string(s) start
    set length string_length(s)
    if length == 0 start
        return 0
    end
    set idx 0
    set newlines 0
    while idx < length start
        if string_substring(s, idx, 1) == "\n" start
            set newlines math_add(newlines, 1)
        end
        set idx math_add(idx, 1)
    end
    return math_add(newlines, 1)
end

function count_lines(path) start
    set items list_dir(path)
    if is_null(items) start
        set content read_file(path)
        if is_null(content) start
            return 0
        end
        return count_lines_in_string(content)
    end

    set total 0
    for name in items start
        set full string_concat(path, string_concat("/", name))
        set subtotal count_lines(full)
        set total math_add(total, subtotal)
    end
    return total
end

set total_lines count_lines("./src")
print string_concat("Total lines in codebase: ", string_from_number(total_lines)) 